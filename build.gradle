plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'java-library'
    id "com.github.erdi.webdriver-binaries" version "2.7"
    id 'maven-publish'
}

group = 'cn.bobdeng.rbac'
version = '0.1'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    providedRuntime
    cucumberRuntime {
        extendsFrom testImplementation
    }
}
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api("org.springframework.boot:spring-boot-starter")
    api 'org.springframework.boot:spring-boot-starter-hateoas'
    api 'org.springframework.boot:spring-boot-starter-jersey'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api("org.springframework.security:spring-security-core:5.3.2.RELEASE")
    api 'org.springframework.boot:spring-boot-starter-thymeleaf:2.7.2'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.0'
    api('org.flywaydb:flyway-mysql:8.5.12')
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    api 'org.apache.commons:commons-lang3:3.12.0'
    api("com.google.guava:guava:31.1-jre")
    api('io.jsonwebtoken:jjwt:0.9.1')
    api 'com.google.code.gson:gson:2.9.0'
    api 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.seleniumhq.selenium:selenium-java:2.45.0'
    implementation 'org.testcontainers:mysql:1.17.3'
    implementation 'org.testcontainers:testcontainers:1.17.3'
}
tasks.named('test') {
    useJUnitPlatform()
}
task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    main = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    environment "SPRING_PROFILES_ACTIVE", "test"
    args = ['--plugin', 'pretty', '--glue', 'com.odde', 'src/test/resources']
}
webdriverBinaries {
    chromedriver {
        version = '103.0.5060.134'
        fallbackTo32Bit = true
    }
    driverUrlsConfiguration = resources.text.fromFile('src/test/resources/webdriver_download_config.json')
    configureTask(cucumber)
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cn.bobdeng.rbac'
            artifactId = 'rbac-server'
            version = '0.4'
            from components.java
        }
    }
}
jar {
    enabled = true
    archiveClassifier = '' //use empty string
}
bootJar{
    enabled = false
}